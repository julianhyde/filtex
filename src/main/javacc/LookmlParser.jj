/*
 * Licensed to Julian Hyde under one or more contributor license
 * agreements.  See the NOTICE file distributed with this work
 * for additional information regarding copyright ownership.
 * Julian Hyde licenses this file to you under the Apache
 * License, Version 2.0 (the "License"); you may not use this
 * file except in compliance with the License.  You may obtain a
 * copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND,
 * either express or implied.  See the License for the specific
 * language governing permissions and limitations under the
 * License.
 */

options {
  STATIC = false;
  IGNORE_CASE = true;
  UNICODE_INPUT = true;
}

PARSER_BEGIN(LookmlParserImpl)

package net.hydromatic.filtex.lookml.parse;

import net.hydromatic.filtex.ast.Pos;
import net.hydromatic.filtex.lookml.ListHandler;
import net.hydromatic.filtex.lookml.ObjectHandler;

import org.checkerframework.checker.nullness.qual.Nullable;

import org.slf4j.Logger;
import org.slf4j.LoggerFactory;

import java.math.BigDecimal;
import java.util.ArrayList;
import java.util.List;
import java.util.Locale;
import java.util.Set;
import java.util.regex.Pattern;

/**
 * Parser for filter expressions, generated from LookmlParser.jj by JavaCC.
 *
 * <p>Limitations:
 * <ul>
 * <li>Comments can only occur in objects and in lists
 *     (not, say, before a comma in a list)
 * <li>No raw string literals
 * <li>Backslash double-quote is the only escape in string literals
 * <li>Identifiers can only be single-part (e.g. 'orders' but not
       'orders.order_date')
 * <li>Lists cannot contain pairs
 * </ul>
 */
public class LookmlParserImpl implements LookmlParser {
  private static final Logger LOGGER =
      LoggerFactory.getLogger("net.hydromatic.filtex.lookml.parse");

  public void setTabSize(int tabSize) {
    jj_input_stream.setTabSize(tabSize);
  }

  public void setCodePropertyNames(Set<String> codePropertyNames) {
    this.codePropertyNames = codePropertyNames;
  }

  Set<String> codePropertyNames;
  String file; // for future multi-file support
  int lineOffset; // for future multi-file support

  public Pos pos() {
    return new Pos(file,
        token.beginLine - lineOffset, token.beginColumn,
        token.endLine - lineOffset, token.endColumn + 1);
  }
}

PARSER_END(LookmlParserImpl)

/* For Debug */
JAVACODE
void debug_message1()
{
  LOGGER.info("{} , {}", getToken(0).image, getToken(1).image);
}

// Atoms and literals

BigDecimal number() : {}
{
  <NUMBER_LITERAL> { return new BigDecimal(token.image); }
}

String string() : {}
{
  <QUOTED_STRING> { return token.image.substring(1, token.image.length() - 1); }
}

String identifier() : {}
{
  <IDENTIFIER> { return token.image; }
}

String comment() : {}
{
  <COMMENT> { return token.image; }
}

/** Parses a document, e.g. 'model: m {}'. */
void document(ObjectHandler h) : {}
{
  ( commentInObj(h) )*
  property(h)
  ( commentInObj(h) )*
}

/** Parses the body of an object. */
void objectBody(ObjectHandler h) : {}
{
  ( objectElement(h) )*
}

/** Parses an element (property or comment) in an object. */
void objectElement(ObjectHandler h) : {}
{
  LOOKAHEAD(1)
  commentInObj(h)
|
  property(h)
}

/** Parses the body of a list.
    It is a sequence of values, separated by commas.
    Any number of comments can occur before or after values. */
void listBody(ListHandler h) : {}
{
  ( commentInList(h) )*
  [
    value(h)
    ( commentInList(h) )*
    (
      <COMMA>
      ( commentInList(h) )*
      value(h)
      ( commentInList(h) )*
    )*
  ]
}

/** Parses a property, e.g. 'p: true', 'q: 1', 'r: []', 's: name {}',
  * 't: {}', 'u: "string"'. */
void property(ObjectHandler h) : {
  String propertyName;
}
{
  propertyName = identifier()
  <COLON>
  {
    if (codePropertyNames.contains(propertyName)) {
      token_source.SwitchTo(IN_CODE);
    }
  }
  (
    codeProperty(propertyName, h)
  |
    identifierProperty(propertyName, h)
  |
    numberProperty(propertyName, h)
  |
    stringProperty(propertyName, h)
  |
    listProperty(propertyName, h)
  |
    objectProperty(propertyName, h)
  )
}

/** Parses a code property, e.g. 'sql: VALUES 1;;'.
 *
 * <p>Must be in the {@link #IN_CODE} lexical state. */
void codeProperty(String propertyName, ObjectHandler h) : {
  StringBuilder b = new StringBuilder();
}
{
  ( <CODE> { b.append(token.image); } )*
  <END_CODE> {
    h.code(propertyName, b.toString());
  }
}

/** Parses a number property, e.g. 'p: 1', 'q: -2', 'r: -3.5'. */
void numberProperty(String propertyName, ObjectHandler h) : {
  BigDecimal number;
}
{
  number = number() {
    h.number(propertyName, number);
  }
}

/** Parses a string property, e.g. p: 'abc'. */
void stringProperty(String propertyName, ObjectHandler h) : {
  String string;
}
{
  string = string() {
    h.string(propertyName, string);
  }
}

/** Parses a list property, e.g. 'p: [1, 2]'. */
void listProperty(String propertyName, ObjectHandler h) : {
  ListHandler h2;
}
{
  <LBRACKET> { h2 = h.listOpen(propertyName); }
  listBody(h2)
  <RBRACKET> { h2.close(); }
}

/** Parses an identifier, enum, boolean or named-object property,
  * e.g. 'x: true', 'y: no', 'v: orders', 'type: inner_join',
  * 'dimension: d {}'. At this stage we don't know the enum
  * values, so we treat them all as identifiers. */
void identifierProperty(String propertyName, ObjectHandler h) : {
  String identifier;
  ObjectHandler h2;
}
{
  identifier = identifier()
  (
    LOOKAHEAD(1)
    <LBRACE> { h2 = h.objOpen(propertyName, identifier); }
    objectBody(h2)
    <RBRACE> { h2.close(); }
  |
    /* empty */
    { h.identifier(propertyName, identifier); }
  )
}

/** Parses an object property, e.g. 'x: {y: 2}'. */
void objectProperty(String propertyName, ObjectHandler h) : {
  ObjectHandler h2;
}
{
  <LBRACE> { h2 = h.objOpen(propertyName); }
  objectBody(h2)
  <RBRACE> { h2.close(); }
}

/** Parses a value in a list, e.g. '1', 'abc', '"abc"', '[1, [2], []]'. */
void value(ListHandler h) : {
  BigDecimal number;
  String string;
  String identifier;
  ListHandler h2;
}
{
  number = number() { h.number(number); }
|
  string = string() { h.string(string); }
|
  identifier = identifier()
  (
     <COLON> string = string() { h.pair(identifier, string); }
  |
     { h.identifier(identifier); }
  )
|
  <LBRACKET> { h2 = h.listOpen(); }
  listBody(h2)
  <RBRACKET> { h2.close(); }
}

/** Parses a comment inside an object. */
void commentInObj(ObjectHandler h) : {
  String comment;
}
{
  comment = comment() { h.comment(comment); }
}

/** Parses a comment inside a list. */
void commentInList(ListHandler h) : {
  String comment;
}
{
  comment = comment() { h.comment(comment); }
}

/* LITERALS */

<DEFAULT> TOKEN :
{
  < NUMBER_LITERAL: ("-")?
      (["0"-"9"])+
      ("." (["0"-"9"])+)?
      (["e", "E"] ("-")?(["0"-"9"])+)? >
| < QUOTED_STRING: <DOUBLE_QUOTE> ( (~["\"", "\\"]) | ("\\" "\"") | ("\\" "\\") )* <DOUBLE_QUOTE> >
| < DOUBLE_QUOTE: "\"" >

| < IDENTIFIER: ["a"-"z", "A"-"Z"](["a"-"z", "A"-"Z", "_", "0"-"9"])* >

| < COMMENT: "#" (~["\n", "\r"])* >
}

<DEFAULT> SKIP :
{ " "
| "\t"
| "\n"
| "\r"
| "\f"
}

<DEFAULT> TOKEN :
{
  < #WHITESPACE: [ " ","\t","\n","\r","\f" ] >
}

<DEFAULT> TOKEN :
{
  < COMMA: "," >
| < LBRACE: "{" >
| < RBRACE: "}" >
| < LBRACKET: "[" >
| < RBRACKET: "]" >
| < COLON: ":" >
}

// A separate lexical state for parsing code blocks, e.g. "sql: ...;;"
// The parser enters the IN_CODE lexical state after seeing the "sql"
// identifier. Seeing ";;" leaves the lexical state.
<IN_CODE> TOKEN :
{
  < END_CODE: ";;" > : DEFAULT
| < CODE: ~[] >
}

// End LookmlParser.jj
